#CLASS {repop} {READ} {Utilities/repop.dat}
#IF {! &repop_data} {
    #CLASS {repop} {OPEN};
    #VARIABLE {repop_data} {{Area}{{number}{}{mean}{}{min}{}{max}{}{last_repop}{}}};
    #VARIABLE {last_repop_area} {New Darkhaven};
    #CLASS {repop} {CLOSE};
}

#CLASS {utilities} {OPEN}
#ALIAS {repop} {
    #SHOWME {Area: %0};
    #CLASS {repop} {READ} {Utilities/repop.dat};
    #FORMAT {repop_time} {%T};
    #VARIABLE {repop_duration} {@get_tdiff{$repop_data[%0][last_repop]}};
    #IF {&repop_data[%0] && ! &repop_data[%0][number]} {
        #NOP {An area's first logged repop};
        #MATH {repop_data[%0][mean]} {@get_tdiff{$repop_data[%0][last_repop]}};
        #VARIABLE {repop_data[%0][min]} {$repop_data[%0][mean]};
        #VARIABLE {repop_data[%0][max]} {$repop_data[%0][mean]};
        #VARIABLE {repop_data[%0][number]} {1};
    };
    #ELSEIF {120 < $repop_duration && $repop_duration < 1.4*$repop_data[%0][mean]} {
        #NOP {The general case, with some sanity checks};
        #MATH {repop_data[%0][mean]} {$repop_data[%0][mean] + (($repop_duration - $repop_data[%0][mean])/($repop_data[%0][number] + 1.000))};
        #IF {$repop_duration > $repop_data[%0][max]} {#VARIABLE {repop_data[%0][max]} {$repop_duration};};
        #ELSEIF {$repop_duration < $repop_data[%0][min]} {#VARIABLE {repop_data[%0][min]} {$repop_duration};};
        #MATH {repop_data[%0][number]} {$repop_data[%0][number] + 1};
    };
    #VARIABLE {last_repop_area} {%0};
    #VARIABLE {repop_data[%0][last_repop]} {$repop_time};
    #CLASS {repop} {WRITE} {Utilities/repop.dat};
}

#ALIAS {sincerepop} {
    #CLASS {repop} {READ} {Utilities/repop.dat};
    #IF {"%0" == "" && $MSDP_ON} {
        #NOP {Assume current area};
        #VARIABLE {sincerepop_area} {$MSDP_AREA_NAME};
    };
    #ELSEIF {"%0" == ""} {
        #NOP {Assume the last repop seen};
        #VARIABLE {sincerepop_area} {$last_repop_area};
    };
    #ELSE {
        #NOP {Try to find area};
	#VARIABLE {sincerepop_area} {$last_repop_area};
    };
    #SHOWME The last repop for $sincerepop_area happened @parse_tdiff{@get_tdiff{$repop_data[$sincerepop_area][last_repop]}} ago.;
    #UNVARIABLE {sincerepop_area};
}

#ALIAS {lastrepop} {
    #CLASS {repop} {READ} {Utilities/repop.dat};
    #IF {"%0" == "" && $MSDP_ON} {
        #NOP {Assume current area};
        #VARIABLE {lastrepop_area} {$MSDP_AREA_NAME};
    };
    #ELSEIF {"%0" == ""} {
        #NOP {Assume the last repop seen};
        #VARIABLE {lastrepop_area} {$last_repop_area};
    };
    #ELSE {
        #NOP {Try to find area};
	#VARIABLE {lastrepop_area} {$last_repop_area};
    };
    #SHOWME The last repop for $lastrepop_area happened at @epoch2time{$repop_data[$lastrepop_area][last_repop];};
    #UNVARIABLE {lastrepop_area};
}
#CLASS {utilities} {CLOSE}
